import org.apache.tools.ant.taskdefs.condition.Os
import java.nio.file.Files;
import java.nio.file.Path;

plugins {
    id "java"
    // moditect for module-info.class injection
    id "org.moditect.gradleplugin" version "1.0.0-rc3"
}

version = '1.0'

def static checkPath(String p) {
    if (!Files.exists(Path.of(p))) {
        throw new IllegalArgumentException("Error: the path ${p} does not exist");
    }
}

def llvm_home = project.property("llvm_home")
checkPath(llvm_home)
checkPath("${llvm_home}/lib/clang")
def clang_versions = new File("${llvm_home}/lib/clang/").list();
if (clang_versions.length == 0) {
    throw new IllegalArgumentException("Could not detect clang version." +
            " Make sure a ${llvm_home}/lib/clang/<VERSION> directory exists")
}
def clang_version = clang_versions[0]

def jextract_app_dir = "$buildDir/jextract"
def clang_include_dir = "${llvm_home}/lib/clang/${clang_version}/include"
checkPath(clang_include_dir)
def os_lib_dir = Os.isFamily(Os.FAMILY_WINDOWS)? "bin" : "lib"
def libclang_dir = "${llvm_home}/${os_lib_dir}"
checkPath(libclang_dir)

repositories {
    mavenCentral()
}

dependencies {
    implementation "net.sf.jopt-simple:jopt-simple:5.0.4"
}

compileJava {
    options.compilerArgs.addAll(['--release', '18'])
    options.compilerArgs << "--add-modules=jdk.incubator.foreign"
    options.compilerArgs << "-Xlint:all"
    options.fork = true
    options.forkOptions.executable = "${jdk18_home}/bin/javac"
}

jar {
    archiveBaseName = 'jextract'
    archiveVersion = project.version
}

moditect {
    // moditect uses java.home to find JDK tools
    System.setProperty("java.home", "$jdk18_home")

    // inject module-info.class for org.openjdk.jextract module
    addMainModuleInfo {
        dependsOn build

        jvmVersion = '18'
        version = project.version
        module {
            moduleInfoSource = '''
                module org.openjdk.jextract {
                    requires transitive java.compiler;
                    requires transitive jdk.incubator.foreign;
                    requires jopt.simple;
                    requires java.prefs;
                    exports org.openjdk.jextract;

                    provides java.util.spi.ToolProvider with
                       org.openjdk.jextract.JextractTool.JextractToolProvider;
                }
                '''
        }
    }

    // inject module-info.class for jopt.simple module
    addDependenciesModuleInfo {
        jvmVersion = '18'
        outputDirectory = file("$buildDir/modules")
        modules {
            module {
                artifact 'net.sf.jopt-simple:jopt-simple:5.0.4'
                moduleInfoSource = '''
                    module jopt.simple {
                        exports joptsimple;
                        exports joptsimple.util;
                    }
                '''
            }
        }
    }
}

task createJextractImage(type: Exec) {
    dependsOn addMainModuleInfo, addDependenciesModuleInfo

    onlyIf { !new File("$buildDir/jextract-jdk-image").exists() }

    executable = "${jdk18_home}/bin/jlink"
    args = [
         "--module-path=$buildDir/modules",
         "--add-modules=org.openjdk.jextract,jdk.compiler",
         "--output=${jextract_app_dir}",
         "--launcher=jextract=org.openjdk.jextract/org.openjdk.jextract.JextractTool",
         "--add-options",
         "\"--enable-native-access=org.openjdk.jextract\""
    ]
}

task copyLibClang(type: Copy) {
    dependsOn createJextractImage

    into("$buildDir")

    from("${libclang_dir}") {
        include("*clang.*")
        include("libLLVM.*")
        into("jextract/${os_lib_dir}")
    }

    from("$clang_include_dir") {
        include("*.h")
        into("jextract/conf/jextract")
    }
}

// jextract jdk image
task jextractapp() {
    dependsOn copyLibClang
}

// very simple integration test for generated jextract
task verify(type: Exec) {
    dependsOn jextractapp

    executable = "${jextract_app_dir}/bin/jextract"
    args = [ "test.h", "-d", "$buildDir/integration_test" ]
}

// jlink a JDK image with org.openjdk.jextract for testing
task createRuntimeImageForTest(type: Exec) {
    dependsOn verify

    onlyIf { !new File("$buildDir/jextract-jdk-test-image").exists() }
    executable = "${jdk18_home}/bin/jlink"
    args = [
         "--module-path=$buildDir/modules" + File.pathSeparator + "$jdk18_home/jmods",
         "--add-modules=ALL-MODULE-PATH",
         "--output=$buildDir/jextract-jdk-test-image",
    ]
}

task prepareTestJDK(type: Copy) {
    dependsOn createRuntimeImageForTest

    into("$buildDir")

    from("${libclang_dir}") {
        include("*clang.*")
        include("libLLVM.*")
        into("jextract-jdk-test-image/${os_lib_dir}")
    }

    from("$clang_include_dir") {
        include("*.h")
        into("jextract-jdk-test-image/conf/jextract")
    }
}

task cmakeConfigure(type: Exec) {
    executable = "cmake"
    args = [
        "-B", "$buildDir/testlib-build",
        "-S", "$projectDir/test/test-support",
        "-DTEST_SOURCE_ROOT:FILEPATH=$projectDir/test",
        "-DCMAKE_BUILD_TYPE:STRING=Release",
        "-DCMAKE_INSTALL_PREFIX:FILEPATH=$buildDir/testlib-install"
    ]
}

task cmakeBuild(type: Exec) {
    dependsOn cmakeConfigure

    executable = "cmake"
    args = [
        "--build", "$buildDir/testlib-build",
        "--config", "Release",
        "--target", "install"
    ]
}

// run jtreg tests. Note: needs jtreg_cmd variable set to point to the jtreg
task jtreg(type: JavaExec) {
    dependsOn prepareTestJDK,cmakeBuild

    workingDir = "$buildDir"

    classpath = files(findProperty("jtreg_home") + "/lib/jtreg.jar")

    args = [
            "-jdk", "$buildDir/jextract-jdk-test-image",
            "-nativepath:$buildDir/testlib-install/${os_lib_dir}",
            "-javaoption:--enable-native-access=org.openjdk.jextract,ALL-UNNAMED",
            "-avm", "-conc:auto", "-verbose:summary",
            "../test"
    ]
}
